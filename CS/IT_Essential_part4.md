# Semantic Versioning Specification

> https://semver.org/

- Semantic Versioning specification(SemVer)
  - 소프트웨어의 버전 번호를 어떻게 정하고, 증가시킬지에 관한 명세이다.
  - Gravatars의 창시자이자 github 공동 창업자인 Tom Preston Werner가 작성했다.
  - 만든 이유
    - 시스템의 규모가 커지고 의존성이 높아질수록, 시스템을 구성하는 각기 다른 패키지들의 버전을 관리하는 것이 힘들어진다.
    - 의존성 관리를 너무 엄격하게 하면 의존하는 모든 패키지의 새 버전을 배포하지 않고는 업데이트를 할 수 없게 되고, 너무 느슨하게 관리하면, 서로 버전이 엉켜 문제가 발생한다.
    - 따라서, 버전 번호를 어떻게 정하고 증가시킬지를 명시함으로써 의존성 관리를 보다 쉽게 하는 데 그 목적이 있다.
  - 2012년 처음 작성되어 현재 널리 사용되고 있다.
    - Node의 npm이 대표적이다.
    - 모든 개념을 베르너가 처음 만든 것은 아니고, 기존 오픈소스 커뮤니티들에서 널리 사용되던 방식들을 취합하여 작성한 것이다.



- 명세
  - 유의적 버전을 사용하는 소프트웨어는 반드시 공개 API를 선언한다.
    - 이 API는 코드 자체로 선언하거나 문서로 엄격히 명시해야한다.
    - 어떤 방식으로든, 정확하고 이해하기 쉬워야 한다.
  - 버전 번호는 X.Y.Z의 형태로 한다. 
    - X,Y,Z는 각각 음이 아닌 정수이다. 
    - 절대로 0이 앞에 붙어서는 안된다(e.g. 01과 같이 사용하지 않는다).
    - X는 주, Y는 부, Z는 수버전을 의미한다.
    - 각각은 반드시 증가하는 수여야한다(즉, 감소해서는 안된다).
  - 특정 버전으로 패키지를 배포하고 나면, 그 버전의 내용은 절대 변경하지 말아야한다.
    - 변경분이 있다면 반드시 새로운 버전으로 배포하도록 한다.
  - 주버전 0(0.Y.Z)은 초기 개발을 위해서 사용한다.
    - 이 공개 API는 안정판으로 보지 않는게 좋다.
  - 1.0.0 버전은 공개 API를 정의한다.
    - 이후으 버전 번호는 이때 배포한 공개 API에서 어떻게 변경되는지에 따라 올린다.
  - 수버전 Z(x.y.Z | x > 0)는 반드시 그전 버전 API와 호환되는 버그 수정의 경우에만 올린다.
    - 버그 수정은 잘못된 내부 기능을 고치는 것을 의미한다.
  - 공개 API에 기존과 호환되는 새로운 기능을 추가할 때는 부버전 Y(x.Y.z | x > 0)를 올린다.
    - 공개 API의 일부가 앞으로 deprecate 될 것으로 표시한 경우에도 반드시 올려야한다.
    - 내부 비공개 코드에 새로운 기능이 대폭 추가되었거나 개선사항이 있을 때도 올릴 수 있다.
    - 부버전을 올릴 때 수버전을 올릴 때 만큼의 변화를 포함할 수도 있다.
    - 부버전이 올라가면 수버전은 반드시 0에서 다시 시작한다.
  - 공개 API에 기존과 호환되지 않는 변화가 있을 때는 반드시 주버전(X,Y,Z | X>0)을 올린다.
    - 부버전이나 수버전급 변화를 포함할 수 있다.
    - 주버전을 올릴 때는 반드시 부버전과 수버전을 0으로 초기화한다.
  - 수버전 바로 뒤에 `-`를 붙이고, 마침표로 구분된 식별자를 더해서 정식 정식 배포를 앞둔(pre-release) 버전을 표기할 수 있다.
    - 식별자는 반드시 아스키문자, 숫자, `-`만으로 구성한다(`[0-9A-Za-z-]`).
    - 식별자는 반드시 한 글자 이상으로 한다.
    - 숫자 식별자의 경우 절대 앞에 0을 붙인 숫자로 표기하지 않는다.
    - 정식배포 전 버전은 관련한 보통 버전보다 우선순위가 낮다.
    - 정식배포 전 버전은 아직 불안정하며 연관된 일반 버전에 대하 호환성 요구사항이 충족되지 않을 수도 있다.
  - 빌드 메타데이터는 수버전이나 정식배포 전 식별자 뒤에 `+`기호를 붙인 뒤에 마침표로 구분된 식별자를 덧붙여서 표현할 수 있다.
    - 식별자는 반드시 아스키 문자와 숫자, `-`만으로 구성한다(`[0-9A-Za-z-]`).
    - 식별자는 반드시 한 글자 이상으로 한다.
    - 빌드 메타데이터는 버전 간의 우선순위를 판단하고자 할 때 만드시 무시해야한다.
    - 그러므로, 빌드 메타데이터만 다른 두 버전의 우선순위는 갖다.
  - 우선순위는 버전의 순서를 정령할 때 서로를 어떻게 비교할지를 나타낸다.
    - 우선순위는 반드시 주, 부, 수 버전, 그리고 정식 배포 전 버전의 식별자를 나누어 계산한다(빌드 메타데이터는 우선순위에 영향을 주지 않는다).
    - 우선순위는 다음의 순서로 차례로 비교하면서, 차이가 나는 부분이 나타나면 결정된다.
    - 주, 부, 수는 숫자로 비교한다.
    - 주, 부, 수 버전이 같을 경우, 정식 배포 전 버전이 표기된 경우의 우선순위가 더 낮다(e.g. 1.0.0-alpha < 1.0.0).
    - 주, 부, 수 ㅈ버전이 같은 두 배포 전 버전 간의 우선순위는 반드시 마침표로 구분된 식별자를 가각 차례로 비교하면서 차이를 찾는다(숫자로만 구성된 식별자는 수의 크기로 비교하고 알파벳이나 `-`가 포함된 경우에는 아스키 문자열 정렬을 하도록 한다).
    - 숫자로만 구성된 식별자는 어떤 경우에도 문자와 `-`가 있는 식별자보다 낮은 우선순위로 여겨진다.
    - 앞선 식별자가 모두 같은 배포 전 버전의 경우에는 필드 수가 많은 쪽이 더 높은 우선순위를 가진다.



- 유의적 버전의 BNF(Backus-Naur Form) 문법

  ```
  <유의적 버전> ::= <버전 몸통>
               | <버전 몸통> "-" <배포 전 버전>
               | <버전 몸통> "+" <빌드>
               | <버전 몸통> "-" <배포 전 버전> "+" <빌드>
  
  <버전 몸통> ::= <주> "." <부> "." <수>
  
  <주> ::= <숫자 식별자>
  
  <부> ::= <숫자 식별자>
  
  <수> ::= <숫자 식별자>
  
  <배포 전 버전> ::= <마침표로 구분된 배포 전 식별자들>
  
  <마침표로 구분된 배포 전 식별자들> ::= <배포 전 식별자>
                                | <배포 전 식별자> "." <마침표로 구분된 배포 전 식별자들>
  
  <빌드> ::= <마침표로 구분된 빌드 식별자들>
  
  <마침표로 구분된 빌드 식별자들> ::= <빌드 식별자>
                              | <빌드 식별자> "." <마침표로 구분된 빌드 식별자들>
  
  <배포 전 식별자> ::= <숫자와 알파벳으로 구성된 식별자>
                  | <숫자 식별자>
  
  <빌드 식별자> ::= <숫자와 알파벳으로 구성된 식별자>
               | <숫자들>
  
  <숫자와 알파벳으로 구성된 식별자> ::= <숫자 아닌 것>
                               | <숫자 아닌 것> <식별자 문자들>
                               | <식별자 문자들> <숫자 아닌 것>
                               | <식별자 문자들> <숫자 아닌 것> <식별자 문자들>
  
  <숫자 식별자> ::= "0"
               | <양의 숫자>
               | <양의 숫자> <숫자들>
  
  <식별자 문자들> ::= <식별자 문자>
                 | <식별자 문자> <식별자 문자들>
  
  <식별자 문자> ::= <숫자>
               | <숫자 아닌 것>
  
  <숫자 아닌 것> ::= <문자>
                | "-"
  
  <숫자들> ::= <숫자>
           | <숫자> <숫자들>
  
  <숫자> ::= "0"
          | <양의 숫자>
  
  <양의 숫자> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
  
  <문자> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
          | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
          | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d"
          | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n"
          | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x"
          | "y" | "z"
  ```

